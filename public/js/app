class Object
  constructor: (@el) ->

  color: (color) ->
    if color?
      @el.css("background-color", color)
    else
      @el.css("background-color")

  x: (x) ->
    @el.css("left", x + "px") if x?    
    parseFloat @el.css "left"

  y: (y) ->
    @el.css("bottom", y + "px") if y?
    parseFloat @el.css "bottom"

  move: (speed) ->
    speed = speed || @speed || x: 0, y: 0
    
    finalY = @y() + speed.y * @delta / 1000
    finalX = @x() + speed.x * @delta / 1000            
                
    if finalY < 0 
      @y(0)
      @collisionY()
    else if finalY + @height() > pongScreen.height
      @y pongScreen.height - @height()
      @collisionY()
    else
      @y finalY
      
    if finalX < 0 
      @x(0)
      @collisionX(leftPlayer)
    else if finalX + @width() > pongScreen.width
      @x pongScreen.width - @width()
      @collisionX(rightPlayer)
    else
      @x finalX

  update: ->
    if !@updated?
      @updated = Date.now()
      return

    @delta = Date.now() - @updated

    if !events[keyCodes.p]
      @_update()

    @updated = @updated + @delta

  _update: ->

  width: ->
    @el.width()

  height: ->
    @el.height()

  left: ->
    @x()

  right: ->
    @x() + @width()

  up: ->
    @y() + @height()

  down: ->
   @y()

  checkCollision: (o) ->
    if(o.left() > @right() || o.right() < @left() || o.down() > @up() || o.up() < @down())
      return false
    return true
    
  collisionX: (p) ->
  
  collisionY: ->


class Player extends Object
  constructor: (@el, @self) ->
    super @el
    @y(pongScreen.height / 2 - @height() / 2)

    if @self?
      @x(20)
      @color("green")
    else
      @x(pongScreen.width - @width() - 20)
      @color("red")

  moveUp: ->
    @move x: 0, y: 400

  moveDown: ->
    @move x: 0, y: -400

  _update: ->
    if @self?
      if events[keyCodes.w]
        @moveUp()
      if events[keyCodes.s]
        @moveDown()
    else
      if events.other[keyCodes.w]
        @moveUp()
      if events.other[keyCodes.s]
        @moveDown()



class Ball extends Object
  constructor: (@el, @speed) ->
    super @el
    @y(pongScreen.height / 2)
    @x(pongScreen.width /2)

  _update: ->    
    @speed.x = - @speed.x for object in objects when (!(object instanceof Ball) && @checkCollision(object)) isnt false 
    @move()

  collisionX: (p) ->
    @speed.x = - @speed.x
    if p == leftPlayer
      scores[rightPlayer]++
      @restart()
    else if p == rightPlayer
      scores[leftPlayer]++
      @restart()
    
  collisionY: ->
    @speed.y = - @speed.y
    
  restart: ->
    $("#score").text(scores[0] + " - " + scores[1]); 
    @y(pongScreen.height / 2)
    @x(pongScreen.width /2)



keyCodes =
  w: 87
  s: 83
  p: 80

events = other: {}
pongScreen = width: 854, height: 480
leftPlayer = 0
rightPlayer = 1
scores = [0, 0]
objects = null

$ ->
  objects = [
    new Player($("#player1"), true)
    new Player($("#player2")),
    new Ball($(".ball"), {x: 200, y: -200})
  ]

  paused = $("#paused")
  paused.css("left", pongScreen.width / 2 - paused.width() / 2)

  score = $("#score")
  $("#score").text(scores[0] + " - " + scores[1]);
  score.css("left", pongScreen.width / 2 - score.width() / 2)

  setInterval( ->
    object.update() for object in objects
  , 100 / 6)

  socket = io location.origin

  socket.on "keydown", (keyCode) ->
    events.other[keyCode] = true
    console.log "him: down:", keyCode, Date.now()

  socket.on "keyup", (keyCode) ->
    events.other[keyCode] = false
    console.log "him: up:", keyCode, Date.now()

  $(document).on "keydown", (event) ->
    if not events[event.keyCode] || event.keyCode == keyCodes.p
      events[event.keyCode] = !events[event.keyCode]
      socket.emit "keydown", event.keyCode
      console.log "me: down:", event.keyCode, Date.now()
      
      if event.keyCode == keyCodes.p
        $("#paused").toggle()

  $(document).on "keyup", (event) ->
    if events[event.keyCode] && event.keyCode != keyCodes.p
      events[event.keyCode] = false
      socket.emit "keyup", event.keyCode
      console.log "me: up:", event.keyCode, Date.now()

